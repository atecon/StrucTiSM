set verbose off
include StrucTiSM.gfn

scalar example = 2 # choose an example, from 1 to 7; -1 means "all"
scalar k = 1
bundle SSmod = null

###
### Example 1: The Local Level Model
###

if example == k || example == -1
    open nile.gdt --quiet --preserve

    # this is the "Local Level Model"; it corresponds to
    #
    ## epsilon = 1
    ## trend = 1
    ## slope = 0
    ## seasonal = 0

    epsilon = TRUE
    trend = 1
    slope = 1
    seasonal = 0

    model = STSM_setup(nile, epsilon, trend, slope, seasonal)
    scalar err = STSM_estimate(&model)
    list comps = STSM_components(model)
    series level_se = model.stSE[,1]
    gnuplot nile nile_level --time-series --with-lines \
      --band=nile_level,level_se,1.96 \
      --band-style=fill --output=display
endif
k++

###
### Example 2: The Local Linear Trend Model
###

if example == k || example == -1
    open bikesharing.gdt --quiet --preserve

    bundle self = _(error_in_obseq = TRUE,\
                    type_trend = "stochastic")

    model = STSM_setup(cnt, self)
    scalar err = STSM_estimate(&model)
    list comps = STSM_components(model)
    scatters cnt comps --with-lines --output=display
endif
k++

if example == k || example == -1
    open australia.gdt --quiet --preserve

    # this is the "Local Linear Trend Model"; it corresponds to
    #
    ## epsilon = 1
    ## trend = 1
    ## slope = 1
    ## seasonal = 0
    #
    # the example is the same as in Ignacio's LLTestim sample script

    epsilon = TRUE
    trend = 1
    slope = 0
    seasonal = 1

    model = STSM_setup(IAU, epsilon, trend, slope, seasonal)
    scalar err = STSM_estimate(&model)
    list comps = STSM_components(model)
    scatters IAU comps --with-lines --output=display
endif
k++

###
### Example 3: The Basic Structural Model
###

if example == k || example == -1
    open data9-3.gdt --quiet --preserve

    # this is the "Basic Structural Model"; it corresponds to
    #
    ## epsilon = 1
    ## trend = 1
    ## slope = 1
    ## seasonal = 2
    #
    # the example is the same as in Ignacio's BSMestim sample script

    epsilon = 1   # boolean
    trend = 1     # 1: stochastic, 2: deterministic
    slope = 1     # 0: none, 1: stochastic, 2: deterministic
    seasonal = 2  # 0: none, 1: stochastic (trig), 2: stochastic (dums), 3: deterministic dums

    model = STSM_setup(reskwh, epsilon, trend, slope, seasonal)
    scalar err = STSM_estimate(&model)
    list comps = STSM_components(model)
    comps -= reskwh_slope
    scatters reskwh comps --with-lines --output=display
endif
k++

###
### Example 4: The BSM on the "airline" data, with a few NAs
###

if example == k || example == -1
    open bjg.gdt --quiet --preserve

    loop i = 30..37
        lg[i] = NA
    endloop
    verbose = 3

    epsilon = 1   # boolean
    trend = 1     # 1: stochastic, 2: deterministic
    slope = 1     # 0: none, 1: stochastic, 2: deterministic
    seasonal = 2  # 0: none, 1: stochastic (trig), 2: stochastic (dums), 3: deterministic dums

    model = STSM_setup(lg, epsilon, trend, slope, seasonal)

    verbose = 1
    mapping = 2
    scalar err = STSM_estimate(&model, verbose, mapping)
    list comps = STSM_components(model)
    scatters lg comps --with-lines --output=display
endif
k++

###
### Example 5: Daily data, with exogenous variables
###

if example == k || example == -1
    open gretl_dnl.gdt --quiet --preserve --frompkg=StrucTiSM
    y = log(dnl)

    # create Fourier regressors with yearly period
    series tt = time*2*$pi/365.25
    list FOU = null
    loop i = 1..3
        series cos$i = cos(i * tt)
        series sin$i = sin(i * tt)
        list FOU += cos$i sin$i
    endloop

    epsilon = 1   # boolean
    trend   = 1   # 1: stochastic, 2: deterministic
    slope   = 0   # 0: none, 1: stochastic, 2: deterministic
    seasonal = 3  # 0: none, 1: stochastic (trig), 2: stochastic (dums), 
                  #   3: deterministic dums

    model = STSM_setup(y, epsilon, trend, slope, seasonal, FOU)

    verbose = 1
    mapping = 0
    scalar err = STSM_estimate(&model, verbose, mapping)
    list comps = STSM_components(model)
    scatters y comps --with-lines --output=display
endif
k++

###
### Example 6: Custom model with a largish dataset and subsampling
###

if example == k || example == -1
    clear --dataset
    open fedstl.bin
    data paynsa
    logs paynsa

    smpl 1960:1 ;

    m   = STSM_setup(l_paynsa,0,1,0,2)
    err = STSM_estimate(&m)
    list comps = STSM_components(m)
    scatters l_paynsa comps --with-lines --output=display
endif
k++

###
### Example 7: Shortcut usage and forecasting
###

if example == k || example == -1
    open data9-9.gdt --quiet
    horizon = 8

    smpl ; -horizon

    bundle mod = null
    comps = BSM(nocars, 0, &mod)
    scalar err = STSM_fcast(&mod, horizon)

    smpl full

    series nocars_f = mshape(NA, $nobs-horizon, 1) | mod.fcast
    series fse  = zeros($nobs-horizon, 1) | sqrt(mod.fcastvar)
    list ToPlot = nocars nocars_f

    plot ToPlot
        options time-series with-lines
        literal set title "Actual vs out-of-sample forecast with 90% confidence band"
        options band=nocars_f,fse,1.65 band-style=fill
    end plot --output=display

endif

